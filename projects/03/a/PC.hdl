// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    Register(in=outT2, load=true, out=outT1);
    Inc16(in=outT1, out=incremented);

    // ab: reset, load, inc -> desired behavior
    // 11: 1,*,* -> reset
    // 10: 0,1,* -> load
    // 01: 0,0,1 -> inc
    // 00: 0,0,0 -> register

    Or(a=reset, b=load, out=a);
    Not(in=a, out=notA);
    And(a=inc, b=notA, out=incAndNotA);
    Or(a=reset, b=incAndNotA, out=b);

    Mux4Way16(a=outT1, b=incremented, c=in, d[0..15]=false,
              sel[0]=b, sel[1]=a, out=outT2);

    And16(a=outT1, b=true, out=out);
}
